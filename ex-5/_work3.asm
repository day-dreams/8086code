title 4x4键盘扫描

;;;程序来源 http://www.doc88.com/p-775898015007.html

;;; port address
MY8255_A EQU   0600H    ;OUTPUT,0
MY8255_B EQU   0602H    ;OUTPUT,0
MY8255_C EQU   0604H    ;[OUTPUT,INPUT]
MY8255_CON EQU 0606H


;STACK
SSTACK SEGMENT STACK
    DW 16 DUP(?)
SSTACK ENDS

;DATA
DATA SEGMENT
DTABLE DB 3FH,06H,5BH,4FH,66H,6DH,7DH,07H
       DB 7FH,6FH,77H,7CH,39H,5EH,79H,71H
DATA ENDS

;CODE
CODE SEGMENT
    ASSUME CS:CODE,DS:DATA
START:
    MOV AX,DATA
    MOV DS,AX
    MOV SI,3000H

    ;CLEAR DISPLAY BUFFER
    MOV AL,00H
    MOV [SI],AL
    MOV [SI+1],AL
    MOV [SI+2],AL
    MOV [SI+3],AL
    MOV [SI+4],AL
    MOV [SI+5],AL

    MOV DI,3005H

    ;WRITE CONTROLE WORD
    MOV DX,MY8255_CON
    MOV AL,81H
    0UT DX,AL

BEGIN:
    CALL DIS    ;DISPLAY
    CALL CLEAR  ;CLERA OUPUT
    CALL CCSCAN ;SCAN KEYBOARD
    JNZ INK1
    JMP BEGIN

INK1:
    CALL DIS
    CALL DALLY
    CALL DALLY
    CALL CLEAR
    CALL CCSCAN
    JNZ INK2        ;有键按下
    JMP BEGIN       ;无键按下

INK2:
    MOV CH,0FEH
    MOV CL,00H

COLUM:
    MOV AL,CH
    MOV DX,MY8255_A
    OUT DX,AL
    MOV DX,MY8255_C
    IN AL,DX

L1:
    TEST AL,01H     ;IS L1?
    JNZ L2
    MOV AL,00H
    JMP KCODE
L2:
    TEST AL,02H     ;IS L2?
    JNZ L3
    MOV AL,04H
    JMP KCODE
L3:
    TEST AL,04H     ;IS L3?
    JNZ L4
    MOV AL,08H
    JMP KCODE
L4:
    TEST AL,08H     ;IS L4?
    JNZ NEXT
    MOV AL,0CH

KCODE:
    ADD AL,CL
    CALL PUTBUF
    PUSH AX

KON:
    CALL DIS
    CALL CLEAR
    CALL CCSCAN
    JNZ KON
    POP AX

NEXT:
    INC CL
    MOV AL,CH
    TEST AL,08H
    JZ KERR
    ROL AL,1
    MOV CH,AL
    JMP COLUM

KERR:
    JMP BEGIN

;SUB PROCS
CCSCAN:             ;读列数据al，al :=  !al & 0FH 
    MOV AL,00H
    MOV DX,MY8255_A
    OUT DX,AL
    MOV DX,MY8255_C
    IN AL,DX
    NOT AL
    AND AL,0FH
    RET

CLEAR:              ;熄灭所有数码管
    MOV DX,MY8255_B
    MOV AL,00H
    OUT DX,AL
    RET

DIS:
    PUSH AX
    MOV SI,3000H
    MOV DL,0DFH
    MOV AL,DL
AGAIN:
    PUSH DX
    MOV DX,MY8255_A
    OUT DX,AL
    MOV AL,[SI]
    MOV BX,OFFSET DTABLE
    AND AX,00FFH
    ADD BX,AX
    MOV AL,[BX]
    MOV DX,MY8255_B
    OUT DX,AL
    CALL DALLY
    INC SI
    POP DX
    MOV AL,DL
    TEST AL,01H
    JZ OUT1
    ROR AL,1
    MOV DL,AL
    JMP AGAIN
OUT1:
    POP AX
    RET

DALLY:          ;延时
    PUSH CX
    MOV CX,0006H
T1:
    MOV AX,009FH
T2:
    DEC AX
    JNZ T2
    LOOP T1
    POP CX
    RET

PUTBUF:         ;把al存入放入[di]，di在[3000H,3005H]间循环移动
    MOV SI,DI
    MOV [SI],AL
    DEC DI
    CMP DI,2FFFH
    JNZ GOBACK
    MOV DI,3005H
GOBACK:
    RET

CODE ENDS
    END START